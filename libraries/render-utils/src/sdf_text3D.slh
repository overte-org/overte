<@include gpu/Config.slh@>
<$VERSION_HEADER$>
<!
// <$_SCRIBE_FILENAME$>
//  Generated on <$_SCRIBE_DATE$>
//
//  Created by Sam Gondelman on 3/15/19
//  Copyright 2019 High Fidelity, Inc.
//  Copyright 2024 Overte e.V.
//
//  Distributed under the Apache License, Version 2.0.
//  See the accompanying file LICENSE or http://www.apache.org/licenses/LICENSE-2.0.html
//
!>
<@if not SDF_TEXT3D_SLH@>
<@def SDF_TEXT3D_SLH@>

LAYOUT(binding=0) uniform sampler2D fontTexture;

struct TextParams {
    vec4 bounds;
    vec4 color;

    vec2 unitRange;

    int effect;
    float effectThickness;

    vec3 effectColor;
    float spare;
};

LAYOUT(binding=0) uniform textParamsBuffer {
    TextParams params;
};

<@func declareEvalSDFSuperSampled()@>

#define TAA_TEXTURE_LOD_BIAS    -3.0

const float interiorCutoff = 0.5;
const float smoothStrength = 4.0;

// MSDF logic from: https://github.com/Chlumsky/msdfgen?tab=readme-ov-file#using-a-multi-channel-distance-field
float median(float r, float g, float b) {
    return max(min(r, g), min(max(r, g), b));
}

float screenPxRange(vec2 texCoord) {
    vec2 screenTexSize = vec2(1.0) / fwidth(texCoord);
    return max(0.5 * dot(params.unitRange, screenTexSize), 1.0);
}

vec2 evalSDF(vec2 texCoord, float bias) {
    vec4 msdf = textureLod(fontTexture, texCoord, TAA_TEXTURE_LOD_BIAS);
    float sdf = median(msdf.r, msdf.g, msdf.b);
    float screenPxDistance = screenPxRange(texCoord) * (sdf - (0.5 - bias));
    float opacity = clamp(screenPxDistance + 0.5, 0.0, 1.0);
    return vec2(opacity, msdf.a);
}

vec4 evalSDFColor(vec2 texCoord, vec4 glyphBounds, float isTofu) {
    vec3 color = params.color.rgb;

    if (isTofu > 0.0f) {
        const float OUTLINE_WIDTH = 0.1;
        return vec4(color, any(greaterThan(abs(texCoord - vec2(0.5)), vec2(0.5 - OUTLINE_WIDTH, 0.5 - (glyphBounds.z / glyphBounds.w) * OUTLINE_WIDTH))));
    }

    vec2 sdf = evalSDF(texCoord, 0.0);

    // Outline
    if (params.effect == 1 || params.effect == 2) {
        const float EPSILON = 0.00001;
        float outline = smoothstep(0.0, 1.0, evalSDF(texCoord, params.effectThickness).x) - EPSILON;

        // with or without fill
        if (params.effect == 1) {
            color = params.effectColor;
            sdf.x = max(outline - sdf.x, 0.0);
        } else {
            color = mix(params.effectColor, color, sdf.x);
            sdf.x = max(sdf.x, outline);
        }
    } else if (params.effect == 3) { // Shadow
        // don't sample from outside of our glyph bounds
        sdf.x *= mix(1.0, 0.0, float(clamp(texCoord, glyphBounds.xy, glyphBounds.xy + glyphBounds.zw) != texCoord));

        if (sdf.x < interiorCutoff) {
            color = params.effectColor;
            const float DOUBLE_MAX_OFFSET_PIXELS = 20.0; // must match value in Font.cpp
            // FIXME: TAA_TEXTURE_LOD_BIAS doesn't have any effect because we're only generating one mip, so here we need to use 0, but it should
            // really match the LOD that we use in the textureLod call below
            vec2 textureOffset = vec2(params.effectThickness * DOUBLE_MAX_OFFSET_PIXELS) / vec2(textureSize(fontTexture, 0/*int(TAA_TEXTURE_LOD_BIAS)*/));
            vec2 shadowTexCoords = texCoord - textureOffset;
            sdf.x = evalSDF(shadowTexCoords, 0.0).x;

            // don't sample from outside of our glyph bounds
            sdf.x *= mix(1.0, 0.0, float(clamp(shadowTexCoords, glyphBounds.xy, glyphBounds.xy + glyphBounds.zw) != shadowTexCoords));
        }
    }

    return vec4(color, sdf.x);
}

vec4 evalSDFSuperSampled(vec2 texCoord, vec2 positionMS, vec4 glyphBounds, float isTofu) {
    // Clip to edges. Note: We don't need to check the top edge.
    if ((params.bounds.z > 0.0 && (positionMS.x < params.bounds.x || positionMS.x > (params.bounds.x + params.bounds.z))) ||
        (params.bounds.w > 0.0 && (positionMS.y < params.bounds.y - params.bounds.w))) {
        return vec4(0.0);
    }

    vec4 color = evalSDFColor(texCoord, glyphBounds, isTofu);

    if (params.color.a == 1.0) {
        // Rely on TAA for anti-aliasing but smooth transition when minification
        // to help filtering
        float uvFootprint = length(fwidth(texCoord) * smoothStrength);
        float smoothStart = max(0.0, 0.5 - uvFootprint);
        float smoothEnd = min(1.0, 0.5 + uvFootprint);
        float alpha = max(smoothstep(smoothStart, smoothEnd, color.a), step(interiorCutoff, color.a));

        return vec4(color.rgb, alpha);
    } else {
        return color;
    }
}

<@endfunc@>

<@endif@>

